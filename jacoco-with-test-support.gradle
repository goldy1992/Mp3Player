apply plugin: 'jacoco'

jacoco {
    toolVersion = "$JACOCO_VERSION"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReportDebug(type: JacocoReport, dependsOn: ['test']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*$[0-9].*',
            '**/dagger/**',
            '**/databinding/**'
    ]

    def mainSrc = "$project.projectDir/src/main/java"
    def hiltSrc = "$project.buildDir/generated/source/kapt/unitTestDebug"
    def mainClasses = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/unitTestDebug", excludes: fileFilter)
    def hiltClasses = fileTree(dir: "$project.buildDir/intermediates/javac/unitTestDebug/classes")

    sourceDirectories.from = files([mainSrc, hiltSrc])
    classDirectories.from = files([mainClasses, hiltClasses])
    executionData.from = "$project.buildDir/jacoco/testUnitTestDebugUnitTest.exec"
}