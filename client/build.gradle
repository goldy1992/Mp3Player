apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: rootProject.file("jacoco-with-test-support.gradle")

android {
    compileSdkVersion target_sdk_version
    buildToolsVersion build_tools_version
    testBuildType "debug"

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += ['**/attach_hotspot_windows.dll', 'META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md', 'META-INF/AL2.0', 'META-INF/ASM', 'META-INF/licenses/ASM', 'META-INF/LGPL2.1']
        }
    }


    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        testInstrumentationRunner "com.github.goldy1992.mp3player.client.CustomTestRunner"
        /*makes the Android Test Orchestrator run its "pm clear" command after each test invocation.
        Ensures app's state is completely cleared between tests. */
        testInstrumentationRunnerArguments clearPackageData: 'true'
        consumerProguardFiles 'consumer-rules.pro'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            enableUnitTestCoverage false
            minifyEnabled false
        }

    }
    flavorDimensions flavor_dimen_mode
    productFlavors {
        full {
            dimension = flavor_dimen_mode
        }
    }

    sourceSets {
        String sharedTestSrcDir = 'src/testCommons/java'
        String sharedTestResDir = 'src/testCommons/res'
        full {
            debug {
                test {
                    java.srcDirs += [sharedTestSrcDir]
                    resources.srcDirs += [sharedTestResDir]
                }
                androidTest {
                    java.srcDirs += [sharedTestSrcDir]
                    resources.srcDirs += [sharedTestResDir]
                }
            }
        }
    }

    compileOptions {
        incremental = false
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        useIR = true
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    testOptions {
        //execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = false
            }
            jacoco { // FOR USE IN TEST EXECUTION
                includeNoLocationClasses = true
                includes = ['com/github/goldy1992/mp3player/client/**']
                excludes = EXCLUDE_LIST
            }
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("automation")) {
            if (variant.buildType.name == "release" || variant.buildType.name == "debug") {
                setIgnore(true)
            }
        }
    }
    namespace 'com.github.goldy1992.mp3player.client'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':commons')

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: coroutines_version
    implementation group: 'androidx.annotation', name: 'annotation', version: annotation_version
    implementation group: 'androidx.media', name: 'media', version: media_version
    implementation group: 'org.apache.commons', name: 'commons-io', version: commons_io_version
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: commons_collections4_version
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang_version
    implementation "androidx.window:window:1.0.0-beta04"
    implementation group: 'androidx.lifecycle', name: 'lifecycle-runtime-ktx', version: lifecycle_version
    implementation group: 'androidx.lifecycle', name: 'lifecycle-livedata-ktx', version: lifecycle_version
    implementation group: 'androidx.lifecycle', name: 'lifecycle-viewmodel-compose', version: lifecycle_version
    // compose dependencies
    implementation group: 'androidx.compose.ui', name: 'ui', version: compose_version
    implementation group: 'androidx.compose.ui', name: 'ui-tooling', version: compose_version
    implementation group: 'androidx.compose.foundation', name: 'foundation', version: compose_version
    implementation group: "androidx.compose.foundation", name: "foundation-layout", version: compose_version
    implementation group: 'androidx.compose.material', name:'material', version: compose_version
    implementation group: 'androidx.compose.material', name:'material-icons-core', version: compose_version
    implementation group: 'androidx.compose.material', name:'material-icons-extended', version: compose_version
    implementation group: 'androidx.compose.material3', name: 'material3', version: compose_material3_version
    implementation group: 'androidx.compose.runtime', name: 'runtime-livedata', version: compose_version
    implementation group: 'androidx.compose.runtime', name: 'runtime-rxjava2', version: compose_version
    implementation group: 'androidx.activity', name: 'activity-compose', version: activity_compose_version
    implementation "androidx.navigation:navigation-compose:2.5.0-rc01"
    def accompanist_version = '0.23.0'
    implementation group: "com.google.accompanist", name: "accompanist-pager", version: accompanist_version
    implementation group: "com.google.accompanist", name: "accompanist-insets", version: accompanist_version
    implementation group: "com.google.accompanist", name: "accompanist-pager-indicators", version: accompanist_version
    implementation group: "io.coil-kt", name: "coil-compose", version: "1.3.2"
    def datastore_preferences_version = '1.0.0'
    implementation group: "androidx.datastore", name: "datastore-preferences", version: datastore_preferences_version
    // hilt
    implementation group: 'com.google.dagger', name: 'hilt-android', version: hilt_version
    implementation group: 'androidx.hilt', name: 'hilt-navigation-compose', version: '1.0.0'
    kapt group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version
    kapt group: 'com.google.dagger', name: 'hilt-compiler', version: hilt_version
    // dependencies for debug
    debugImplementation("androidx.compose.ui:ui-test-manifest:$compose_version")
    // for use with compose previews
    debugApi("androidx.customview:customview:1.1.0")
    debugApi("androidx.customview:customview-poolingcontainer:1.0.0-beta02")


    // Android tests
    androidTestImplementation group: 'com.google.dagger', name: 'hilt-android-testing', version: hilt_version
    androidTestImplementation group: 'androidx.compose.ui', name: 'ui-test-junit4', version: compose_version
    // Test rules and transitive dependencies:
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$compose_version")
    androidTestImplementation junitUnitTests
    androidTestImplementation group: 'androidx.test', name: 'rules', version: test_rules_version
    androidTestImplementation group: 'androidx.test.ext', name: 'junit', version: junit_ext_version
    androidTestImplementation group: 'androidx.test', name: 'runner', version: test_runner_version
    androidTestUtil group: "androidx.test", name: "orchestrator", version: test_orchestrator_version
    androidTestImplementation group: 'com.linkedin.dexmaker', name: 'dexmaker-mockito-inline-extended', version: '2.28.1'
    androidTestImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    // Test rules and transitive dependencies:
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$compose_version")
    androidTestImplementation group: 'androidx.test.uiautomator', name: 'uiautomator', version: ui_automator_version

    // Unit test dependencies
    testImplementation("org.robolectric:robolectric:$robolectric_version") {
        exclude group: "com.google.auto.service", module: "auto-service"
    }
    testImplementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-test', version: coroutines_version
    testImplementation junitUnitTests
    testImplementation group: 'com.google.dagger', name: 'hilt-android-testing', version: hilt_version
    kaptTest group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version
}

kapt {
    correctErrorTypes true
}

sonarqube {
    androidVariant 'fullDebug'
    properties {
        property 'sonar.java.binaries', "${project.buildDir}/intermediates/javac/fullDebug/classes,${project.buildDir}/tmp/kotlin-classes/fullDebug"
        property 'sonar.coverage.jacoco.xmlReportPaths', "${project.buildDir}/reports/jacoco/${rootProject.ext.properties.jacaco_report_name}/${rootProject.ext.properties.jacaco_report_name}.xml"
        property 'sonar.sources', "${projectDir}/src/main/java"
    }
}