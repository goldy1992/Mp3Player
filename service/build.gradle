apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion target_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        testInstrumentationRunner "com.github.goldy1992.mp3player.service.CustomTestRunner"
        /*makes the Android Test Orchestrator run its "pm clear" command after each test invocation.
        Ensures app's state is completely cleared between tests. */
        testInstrumentationRunnerArguments clearPackageData: 'true'
        consumerProguardFiles 'consumer-rules.pro'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            getIsDefault().set(true)
            enableUnitTestCoverage true
            testCoverageEnabled true
            minifyEnabled false
        }
    }
    flavorDimensions flavor_dimen_mode
    productFlavors {
        full {
            getIsDefault().set(true)
            dimension = flavor_dimen_mode
        }
        automation {
            dimension = flavor_dimen_mode
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("automation")) {
            if (variant.buildType.name == "release" || variant.buildType.name == "unittest") {
                setIgnore(true)
            }
        }
    }

    compileOptions {
        incremental = false
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions { jvmTarget = "11" }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped"//, "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = false
            }
            jacoco {
                includeNoLocationClasses = true
                includes = ['com/github/goldy1992/mp3player/service/**']
                excludes =  EXCLUDE_LIST
            }
        }
    }
    namespace 'com.github.goldy1992.mp3player.service'
}

dependencies {

    implementation group: 'androidx.media3', name: 'media3-session', version: media3_version
    implementation group: 'androidx.media3', name: 'media3-exoplayer', version: media3_version
    implementation group: 'androidx.media3', name: 'media3-exoplayer-dash', version: media3_version
    implementation group: 'androidx.media3', name: 'media3-ui', version: media3_version
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: commons_collections4_version
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang_version
    implementation group: 'androidx.appcompat', name: 'appcompat', version: app_compat_version
    implementation group: 'androidx.core', name: 'core-ktx', version: androidx_core_ktx_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: coroutines_version
    implementation project(':commons')

    implementation group: 'androidx.room', name: 'room-runtime', version: room_version
    implementation group: 'androidx.room', name: 'room-ktx', version: room_version
    kapt group: 'androidx.room', name: 'room-compiler', version: room_version
    implementation group: 'androidx.concurrent', name: 'concurrent-futures-ktx', version: '1.1.0'
    implementation group: "androidx.datastore", name: "datastore-preferences", version: datastore_preferences_version
    testImplementation junitUnitTests

    testImplementation("org.robolectric:robolectric:$robolectric_version") {
        exclude group: "com.google.auto.service", module: "auto-service"
    }
    //testImplementation group: 'org.robolectric', name: 'shadows-supportv4', version: robolectric_version
    testImplementation group: "org.mockito.kotlin", name: "mockito-kotlin", version: mockito_kotlin_version
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: mockito_inline_version
    testImplementation group: 'androidx.test', name: 'core', version: test_core_version
    testImplementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-test', version: coroutines_version

    // hilt
    implementation group: 'com.google.dagger', name: 'hilt-android', version: hilt_version
    kapt group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version

    testImplementation group: 'com.google.dagger', name: 'hilt-android-testing', version: hilt_version
    kaptTest group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version

    androidTestImplementation group: 'com.google.dagger', name: 'hilt-android-testing', version: hilt_version
    kaptAndroidTest group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version

}

sonarqube {
    androidVariant 'fullDebug'
    properties {
        property 'sonar.java.binaries', "${project.buildDir}/intermediates/javac/fullDebug/classes"
        property 'sonar.java.test.binaries', "${project.buildDir}/intermediates/javac/fullDebugUnitTest/classes, ${project.buildDir}/tmp/kotlin-classes/fullDebugUnitTest"
        property 'sonar.coverage.jacoco.xmlReportPaths', "${project.buildDir}/reports/coverage/test/full/debug/report.xml"
        property 'sonar.junit.reportPaths', "${project.buildDir}/test-results/testFullDebugUnitTest/TEST-*.xml"
    }
}

