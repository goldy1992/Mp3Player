apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'com.github.ben-manes.versions'

android {

    compileSdkVersion TARGET_SDK_VERION
	buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.github.goldy1992.mp3player"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion PIE
        versionCode 4
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
    }

    adbOptions {
        timeOutInMs 20 * 60 * 1000 // 10 minutes
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled = true
        }
    }
    flavorDimensions "default"
    productFlavors {
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            resValue "string", "app_name", "Music Player"
        }
        automation {
            applicationIdSuffix ".automation"
            versionNameSuffix "-automation"
            resValue "string", "app_name", "Test Music Player"

        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
            jacoco {
                includeNoLocationClasses=true
            }
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        disable 'PrivateResource'
        abortOnError false
    }
    compileOptions {
        incremental = false
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = "1.8" }
}

dependencyUpdates {
    checkForGradleUpdate = true
    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')


    implementation group: 'org.apache.commons', name: 'commons-lang3', version: COMMONS_LANG_VERSION
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: COMMONS_COLLECTIONS4_VERSION
    implementation group: 'com.google.guava', name: 'guava', version: GOOGLE_GUAVA_VERSION
    implementation group: 'androidx.media', name: 'media', version: MEDIA_VERSION

    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: KOTLIN_COROUTINE_VERSION
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: KOTLIN_COROUTINE_VERSION

    implementation "androidx.recyclerview:recyclerview:1.1.0"


    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-ui', version: EXO_PLAYER_VERSION

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.3.61'

    // added to remove compile time warning
//    testAnnotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc4'

    //dagger
    implementation group: 'com.google.dagger', name: 'dagger', version: DAGGER_VERSION
    implementation group: 'com.google.dagger', name: 'dagger-android', version: DAGGER_VERSION


    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION
    kaptTest group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION
    annotationProcessor  group: 'com.google.dagger', name: 'dagger-android-processor', version: DAGGER_VERSION
    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION




    // vintage
    testImplementation group: 'junit', name: 'junit', version: JUNIT4_VERSION
    // (Optional) If you also have JUnit 4-based tests
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: JUNIT5_VERSION
    // jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: JUNIT5_VERSION
    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUNIT5_VERSION
    //testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.25.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: MOCKITO_CORE_VERSION
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: COMMONS_LANG_VERSION
    testImplementation group: 'androidx.appcompat', name: 'appcompat', version: APP_COMPAT_VERSION
    testImplementation group: 'org.robolectric', name: 'robolectric', version: ROBOLECTRIC_VERSION// https://mvnrepository.com/artifact/org.robolectric/shadows-support-v4
    testImplementation group: 'org.robolectric', name: 'shadows-supportv4', version: ROBOLECTRIC_VERSION


    debugImplementation group: 'androidx.fragment', name: 'fragment-testing', version: FRAGMENT_VERSION
    androidTestImplementation group: 'androidx.test', name: 'core', version: TEST_CORE_VERSION
    androidTestImplementation group: 'androidx.annotation', name: 'annotation', version: ANNOTATION_VERSION
    androidTestImplementation group: 'androidx.test.ext', name: 'junit', version: JUNIT_EXT_VERSION
    androidTestImplementation group: 'androidx.test', name: 'rules', version: TEST_RULES_VERSION
    androidTestImplementation group: 'androidx.test', name: 'runner', version: TEST_RUNNER_VERSION
    androidTestImplementation group: 'androidx.test', name: 'monitor', version: MIRROR_VERSION
    androidTestImplementation group: 'androidx.test.espresso', name: 'espresso-contrib', version: ESPRESSO_CORE_VERSION

    automationImplementation group: 'androidx.test', name: 'rules', version: TEST_RULES_VERSION
    automationImplementation group: 'androidx.test.espresso', name: 'espresso-core', version: ESPRESSO_CORE_VERSION

    androidTestImplementation(group: 'androidx.test.espresso', name: 'espresso-core', version: ESPRESSO_CORE_VERSION) {
        exclude module: "support-annotations"
    }
    androidTestImplementation group: 'androidx.test.uiautomator', name: 'uiautomator', version: TEST_UI_AUTOMATOR_VERSION
    implementation 'androidx.core:core-ktx:1.2.0-rc01'
    implementation group: 'androidx.appcompat', name: 'appcompat', version: APP_COMPAT_VERSION

    implementation project(path: ':commons')
    api project(path: ':service')
    api project(path: ':client')




}
repositories {
    mavenCentral()
}