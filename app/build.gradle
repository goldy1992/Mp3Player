apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
    compileSdkVersion TARGET_SDK_VERION
	buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "com.github.goldy1992.mp3player"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion PIE
        versionCode 4
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled = true
        }
    }
    flavorDimensions "tier"
    productFlavors {
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            resValue "string", "app_name", "Music Player"
            missingDimensionStrategy flavor_dimen_mode, flavor_full, flavor_integration, flavor_unitTest
        }
        automation {
            applicationIdSuffix ".automation"
            versionNameSuffix "-automation"
            resValue "string", "app_name", "Test Music Player"
            missingDimensionStrategy flavor_dimen_mode, flavor_endToEnd

        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def isReleaseTest = variant.buildType.name == "release" &&
                        names.contains(flavor_automation)

        // To check for a build type instead, use variant.buildType.name == "buildType"
        if (isReleaseTest) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }


    testOptions {

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
            jacoco {
                includeNoLocationClasses=true
            }
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        disable 'PrivateResource'
        abortOnError false
    }
    compileOptions {
        incremental = false
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = "1.8" }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: COMMONS_LANG_VERSION
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: COMMONS_COLLECTIONS4_VERSION
    implementation group: 'com.google.guava', name: 'guava', version: GOOGLE_GUAVA_VERSION
    implementation group: 'androidx.media', name: 'media', version: MEDIA_VERSION
    implementation group: 'androidx.viewpager2', name: 'viewpager2', version: VIEW_PAGER2_VERSION
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: KOTLIN_COROUTINE_VERSION
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: KOTLIN_COROUTINE_VERSION
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.1.0'
    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-ui', version: EXO_PLAYER_VERSION
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'androidx.core', name: 'core-ktx', version: ANDROIDX_CORE_KOTLIN_VERSION
    implementation group: 'androidx.appcompat', name: 'appcompat', version: APP_COMPAT_VERSION
    implementation group: 'com.google.android.material', name: 'material', version: MATERIAL_VERSION
    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-core', version: EXO_PLAYER_VERSION
    implementation group: 'com.google.android.exoplayer', name: 'extension-mediasession', version: EXO_PLAYER_VERSION
    implementation group: 'androidx.room', name: 'room-runtime', version: ROOM_VERSION
    implementation group: 'com.l4digital.fastscroll', name: 'fastscroll', version: FAST_SCROLL_VERSION

    // Glide
    implementation group: 'com.github.bumptech.glide', name: 'glide', version: GLIDE_VERSION

    //dagger
    implementation group: 'com.google.dagger', name: 'dagger', version: DAGGER_VERSION

    // local libs
    implementation project(path: ':commons')
    implementation project(path: ':service')
    implementation project(path: ':client')

    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION
    kaptTest group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION

    androidTestImplementation junit5UnitTests
    testRuntimeOnly junit5UnitTestRuntime

    androidTestImplementation group: 'androidx.test', name: 'core', version: TEST_CORE_VERSION
    androidTestImplementation group: 'androidx.annotation', name: 'annotation', version: ANNOTATION_VERSION
    androidTestImplementation group: 'androidx.test.ext', name: 'junit', version: JUNIT_EXT_VERSION
    androidTestImplementation group: 'androidx.test', name: 'rules', version: TEST_RULES_VERSION
    androidTestImplementation group: 'androidx.test', name: 'runner', version: TEST_RUNNER_VERSION
    androidTestImplementation group: 'androidx.test', name: 'monitor', version: MIRROR_VERSION
    androidTestImplementation group: 'androidx.test.espresso', name: 'espresso-contrib', version: ESPRESSO_CORE_VERSION
    androidTestImplementation group: 'androidx.fragment', name: 'fragment-testing', version: FRAGMENT_VERSION


    androidTestImplementation(group: 'androidx.test.espresso', name: 'espresso-core', version: ESPRESSO_CORE_VERSION) {
        exclude module: "support-annotations"
    }
    androidTestImplementation group: 'androidx.test.uiautomator', name: 'uiautomator', version: TEST_UI_AUTOMATOR_VERSION



    debugImplementation group: 'androidx.core', name: 'core-ktx', version: ANDROIDX_CORE_KOTLIN_VERSION
    debugImplementation group: 'androidx.fragment', name: 'fragment-testing', version: FRAGMENT_VERSION
    implementation group: 'androidx.fragment', name: 'fragment', version: FRAGMENT_VERSION
    debugImplementation group: 'androidx.fragment', name: 'fragment-ktx', version: FRAGMENT_VERSION

}