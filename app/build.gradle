apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: 'dagger.hilt.android.plugin'

android {
    testBuildType "debug"
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
    compileSdkVersion target_sdk_version
	buildToolsVersion build_tools_version
    defaultConfig {
        applicationId "com.github.goldy1992.mp3player"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode Integer.parseInt(project.properties['version.code']).intValue()
        versionName project.properties['version.name']
        testInstrumentationRunner "com.github.goldy1992.mp3player.Mp3PlayerHiltTestRunner"
        missingDimensionStrategy 'fullUnittest', 'fullDebug'
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
        unittest {
            debuggable true
            minifyEnabled false
            matchingFallbacks = ['debug']
        }
    }
    flavorDimensions flavor_dimen_mode
    productFlavors {
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
            resValue "string", "app_name", "Music Player"
        }
        automation {
            applicationIdSuffix ".automation"
            versionNameSuffix "-automation"
            resValue "string", "app_name", "Test Music Player"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("automation")) {
            if (variant.buildType.name == "unittest" || variant.buildType.name == "release") {
                setIgnore(true)
            }
        }
    }


    testOptions {

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
            jacoco {
                includeNoLocationClasses=true
            }
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        disable 'PrivateResource'
        abortOnError false
    }
    compileOptions {
        incremental = false
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions { jvmTarget = "1.8" }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang_version
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: commons_collections4_version
    implementation group: 'androidx.media', name: 'media', version: media_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: coroutines_version
    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-ui', version: exo_player_vesrion
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'androidx.core', name: 'core-ktx', version: androidx_core_ktx_version
    implementation group: 'androidx.appcompat', name: 'appcompat', version: app_compat_version
    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-core', version: exo_player_vesrion
    implementation group: 'com.google.android.exoplayer', name: 'extension-mediasession', version: exo_player_vesrion
    implementation group: 'androidx.room', name: 'room-runtime', version: room_version

    // local libs
    implementation project(path: ':commons')
    implementation project(path: ':service')
    implementation project(path: ':client')

    // hilt
    implementation group: 'com.google.dagger', name: 'hilt-android', version: hilt_version
    kapt group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version
  //  implementation group: 'androidx.hilt', name: 'hilt-lifecycle-viewmodel', version: HILT_JETPACK_VERSION
    kapt group: 'com.google.dagger', name: 'hilt-compiler', version: hilt_version
    androidTestImplementation group: 'com.google.dagger', name: 'hilt-android', version: hilt_version
    androidTestImplementation group: 'com.google.dagger', name: 'hilt-android-testing', version: hilt_version
    kaptAndroidTest group: 'com.google.dagger', name: 'hilt-android-compiler', version: hilt_version
 //   androidTestImplementation group: 'androidx.hilt', name: 'hilt-lifecycle-viewmodel', version: HILT_JETPACK_VERSION

    kaptAndroidTest group: 'com.google.dagger', name: 'hilt-compiler', version: hilt_version


    androidTestImplementation junitUnitTests

    androidTestImplementation group: 'androidx.test', name: 'core', version: test_core_version
    androidTestImplementation group: 'androidx.annotation', name: 'annotation', version: annotation_version
    androidTestImplementation group: 'androidx.test.ext', name: 'junit', version: junit_ext_version
    androidTestImplementation group: 'androidx.test', name: 'rules', version: test_rules_version
    androidTestImplementation group: 'androidx.test', name: 'runner', version: test_runner_version
    androidTestImplementation group: 'androidx.test', name: 'monitor', version: monitor_version
    androidTestImplementation group: 'androidx.test.espresso', name: 'espresso-contrib', version: espresso_core_version


    androidTestImplementation(group: 'androidx.test.espresso', name: 'espresso-core', version: espresso_core_version) {
        exclude module: "support-annotations"
    }
    androidTestImplementation group: 'androidx.test.uiautomator', name: 'uiautomator', version: TEST_UI_AUTOMATOR_VERSION



    debugImplementation group: 'androidx.core', name: 'core-ktx', version: androidx_core_ktx_version

    implementation group: 'androidx.activity', name: 'activity-ktx', version: activity_ktx_version

}

sonarqube {
    skipProject = true
}